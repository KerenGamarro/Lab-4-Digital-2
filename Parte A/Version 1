//Keren Gamarro 23546
//Universidad del Valle de Guatemala
//Regulador de intensidad de 3 leds mediante botones

#include <Arduino.h> //incluimos la libreria para que Visual la pueda usar

// Pines para LEDs
#define LED_ROJO 15 //se deben de conectar a pines que permitan PWM, sino no funciona
#define LED_VERDE 4
#define LED_AZUL 18

// Pines para botones
#define BOTON_B3 33  // Selección de color //con este no importa porque reciben entradas digitales
#define BOTON_B4 32  // Cambio de brillo

// Variables
int colorSeleccionado = 0; // Almacena el color que estoy seleccionando, en este caso 0 = Rojo, 1 = Verde, 2 = Azul 
int nivelesBrillo[3] = {0, 0, 0}; // Nivel (0–3) para cada color 
const int nivelesPWM[4] = {0, 85, 170, 255}; // PWM equivalente, se piden 4 niveles por lo que se coloca su representacion en bits 0-255
const int porcentajeBrillo[4] = {0, 33, 66, 100}; // Porcentaje para mostrar, el equivalente de los bits

// Antirrebote, para que se pueda apreciar los cambios de color
unsigned long tiempoUltimoB3 = 0;
unsigned long tiempoUltimoB4 = 0;
const unsigned long debounceDelay = 200;

// Función auxiliar para imprimir estado, esto en caso el codigo no funcione o de problemas poder verificar que paso
void imprimirEstado() {
  const char* colores[3] = {"Rojo", "Verde", "Azul"};
  Serial.print("Color seleccionado: ");
  Serial.print(colores[colorSeleccionado]);
  Serial.print(" | Brillo: ");
  Serial.print(porcentajeBrillo[nivelesBrillo[colorSeleccionado]]);
  Serial.println("%");
}

void setup() {
  Serial.begin(115200);

  // Configurar PWM
  ledcAttachPin(LED_ROJO, 0); //conectamos un canal PWM a cada pin
  ledcAttachPin(LED_VERDE, 1);
  ledcAttachPin(LED_AZUL, 2);
  ledcSetup(0, 5000, 8); //configuramos la frecuencia y los bits que debe de leer para cada led, 0 1 2
  ledcSetup(1, 5000, 8);
  ledcSetup(2, 5000, 8);

  // Configurar botones como entrada pull-up
  pinMode(BOTON_B3, INPUT_PULLUP); //conectados a tierra
  pinMode(BOTON_B4, INPUT_PULLUP);

  // Mostrar estado inicial
  imprimirEstado();
}

void loop() {
  // Botón B3: Cambiar color seleccionado, va ciclico de rojo verde azul rojo y asi, de ahi que se use %3
  if (digitalRead(BOTON_B3) == LOW && millis() - tiempoUltimoB3 > debounceDelay) {
    colorSeleccionado = (colorSeleccionado + 1) % 3;
    imprimirEstado();
    tiempoUltimoB3 = millis();
  }

  // Botón B4: Cambiar nivel de brillo del color seleccionado, lo mismo solo que como son 4 estados es %4
  if (digitalRead(BOTON_B4) == LOW && millis() - tiempoUltimoB4 > debounceDelay) {
    nivelesBrillo[colorSeleccionado] = (nivelesBrillo[colorSeleccionado] + 1) % 4;
    imprimirEstado();
    tiempoUltimoB4 = millis();
  }

  // Aplicar PWM a cada LED
  ledcWrite(0, nivelesPWM[nivelesBrillo[0]]);
  ledcWrite(1, nivelesPWM[nivelesBrillo[1]]);
  ledcWrite(2, nivelesPWM[nivelesBrillo[2]]);

  delay(10);
}
